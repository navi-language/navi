use std.process;
use std.env;
use std.path;
use std.fs;

test "exec" {
    if (env.platform() == "windows") {
        return;
    }

    let args = [string] { "-c", "echo", "Hello, world!" };
    let envs = <string, string> { "ENV1": "hello", "ENV2": "Foo Bar" };

    let output = process.exec("sh", args: args, envs: envs);
    assert_eq output.status, 0, output;
    assert_eq output.stderr.to_string().trim(), "";
    assert_eq output.stdout.to_string().trim(), "";
}

test "exec status" {
    if (env.platform() == "windows") {
        return;
    }

    let output = process.exec("false");
    assert_eq output.status, 1;

    let output = process.exec("true");
    assert_eq output.status, 0;
}

test "exec stderr" {
    if (env.platform() == "windows") {
        return;
    }

    let output = process.exec("sh", args: [string] { "-c", "echo This message goes to stderr >&2" });
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string().trim(), "This message goes to stderr";
    assert_eq output.stdout.to_string().trim(), "";
}

test "exec with args" {
    if (env.platform() == "windows") {
        return;
    }

    let args = [string] { "arg1", "arg2", "arg3" };
    let output = process.exec("echo", args: args);
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string().trim(), "arg1 arg2 arg3";

    let output = process.exec(
        "sh",
        args: [string] { "-c", `echo "hello world\nnext line." | wc -l` }
    );
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string().trim(), "2";

    let output = process.exec(
        "sh",
        args: [string] { "-c", `echo "hello world" && echo foo bar dar` }
    );
    assert_eq output.status, 0, output.stderr.to_string();
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string().trim(), "hello world\nfoo bar dar";

    let output = process.exec(
        "sh",
        args: [string] { "-c", "echo", `"hello world"`, "&&", "echo foo bar dar" }
    );
    assert_eq output.status, 0, output.stderr.to_string();
    assert_eq output.stdout.to_string().trim(), "";
}

test "exec with env" {
    if (env.platform() == "windows") {
        return;
    }

    let envs = <string, string> { "ENV1": "hello", "ENV2": "Foo Bar" };
    let output = process.exec("printenv", envs: envs);
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";

    let stdout = output.stdout.to_string();
    assert_eq stdout.contains("ENV1=hello"), true, stdout;
    assert_eq stdout.contains("ENV2=Foo Bar"), true, stdout;
}

test "exec stdin" {
    if (env.platform() == "windows") {
        return;
    }

    let filename = path.join(env.tmpdir(), "test-exec-in");
    if (fs.exists(filename)) {
        fs.remove_file(filename);
    }
    let f = fs.create(filename);
    f.write_string("Hello, world!");
    f.flush();

    let output = process.exec("cat", stdin: "null");
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string(), "";

    let output = process.exec("cat", stdin: "piped");
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string(), "";

    let output = process.exec("cat", stdin: filename);
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string(), "Hello, world!";
    fs.remove_file(filename);
}

test "exec stdout" {
    if (env.platform() == "windows") {
        return;
    }

    let output = process.exec("echo", args: [string] { "Hello, world!" }, stdout: "null");
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string().trim(), "";

    let output = process.exec("echo", args: [string] { "Hello, world!" }, stdout: "piped");
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string().trim(), "Hello, world!";

    let f = path.join(env.tmpdir(), "test-exec-stdout");
    if (fs.exists(f)) {
        fs.remove_file(f);
    }
    let output = process.exec("echo", args: [string] { "Hello, world!" }, stdout: f);
    assert_eq output.status, 0, `exec failed, ${output.stderr} file: ${f}`;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string(), "";
    let out = fs.read_to_string(f);
    assert_eq out, "Hello, world!\n", f;

    // test when file exist
    let output = process.exec("echo", args: [string] { "Hello, world!" }, stdout: f);
    assert_eq output.status, 0, `exec failed, ${output.stderr} file: ${f}`;
    let out = fs.read_to_string(f);
    assert_eq out, "Hello, world!\nHello, world!\n", f;
    fs.remove_file(f);
}

test "exec stderr" {
    if (env.platform() == "windows") {
        return;
    }

    let output = process.exec(
        "sh",
        args: [string] { "-c", "echo Hello, world! >&2" },
        stderr: "null"
    );
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string(), "";

    let output = process.exec(
        "sh",
        args: [string] { "-c", "echo Hello, world! >&2" },
        stderr: "piped"
    );
    assert_eq output.status, 0;
    assert_eq output.stderr.to_string().trim(), "Hello, world!";
    assert_eq output.stdout.to_string(), "";

    let f = path.join(env.tmpdir(), "test-exec-stderr");
    if (fs.exists(f)) {
        fs.remove_file(f);
    }
    let output = process.exec("sh", args: [string] { "-c", "echo Hello, world! >&2" }, stderr: f);
    assert_eq output.status, 0, `exec failed, ${output.stderr} file: ${f}`;
    assert_eq output.stderr.to_string(), "";
    assert_eq output.stdout.to_string(), "";
    let out = fs.read_to_string(f);
    assert_eq out, "Hello, world!\n", f;
    fs.remove_file(f);
}

test "exec with cwd" {
    if (env.platform() == "windows") {
        return;
    }

    let cwd = env.tmpdir();
    let full_path = path.absolute(cwd);
    let output = process.exec("pwd", args: [string] { "-L" }, cwd: cwd);
    assert_eq output.status, 0;
    assert_eq output.stdout.to_string().trim(), full_path;
}

test "exec with uid & gid" {
    if (env.platform() == "windows") {
        return;
    }

    // get current uid & gid
    let output = process.exec("id", args: [string] { "-u" });
    assert_eq output.status, 0;
    let uid, _ = output.stdout.to_string().trim().to_int();
    let output = process.exec("id", args: [string] { "-g" });
    assert_eq output.status, 0;
    let gid, _ = output.stdout.to_string().trim().to_int();

    let output = process.exec("id", args: [string] { "-u" }, uid: uid);
    assert_eq output.status, 0;
    assert_eq output.stdout.to_string().trim(), `${uid}`;

    let output = process.exec("id", args: [string] { "-g" }, gid: gid);
    assert_eq output.status, 0;
    assert_eq output.stdout.to_string().trim(), `${gid}`;
}
