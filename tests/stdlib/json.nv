use std.fs;
use std.io.{Bytes, Cursor};
use std.json;
use std.path;
use std.time.DateTime;

type alias MyUnionType = string | int | float | bool;

struct User {
    name: string,
    id: int,
    enabled: bool,
    float: float,
    profile: Profile,
    tags: [string]?,
    null: string?,
    decimal: Decimal,
    datetime: DateTime,
    union_type: MyUnionType,
}

struct Profile {
    city: string,
}

test "parse" {
    // null
    let val = try! json.parse::<string?>(`null`);
    assert_eq val, nil;

    // string
    let val = try! json.parse::<string>(`"foo"`);
    assert_eq val, "foo";

    // bool
    let val = try! json.parse::<bool>(`true`);
    assert_eq val, true;
    let val = try! json.parse::<bool>(`false`);
    assert_eq val, false;

    // int
    let val = try! json.parse::<int>(`123`);
    assert_eq val, 123;

    // float
    let val = try! json.parse::<float>(`123.0`);
    assert_eq val, 123.0;

    // struct
    let user = try! json.parse::<User>(`{
        "name": "Jason Lee",
        "id": 123456,
        "enabled": true,
        "float": 1.23,
        "profile": {
            "city": "Chengdu"
        },
        "tags": null,
        "null": null,
        "decimal": "123.456",
        "datetime": "2024-02-28T09:50:40Z",
        "union_type": "foo"
    }`);
    assert_eq user.name, "Jason Lee";
    assert_eq user.id, 123456;
    assert_eq user.enabled, true;
    assert_eq user.float, 1.23;
    assert_eq user.tags, nil;
    assert_eq user.null, nil;
    assert_eq user.decimal, try! Decimal.from_float(123.456);
    assert_eq user.datetime.to_string(), "2024-02-28T09:50:40Z";
    assert_eq user.profile.city, "Chengdu";
    assert_eq user.union_type.(string), "foo";

    // array
    let val = try! json.parse::<[string]>(`["Navi", "Rust"]`);
    assert_eq val.len(), 2;
    assert_eq val[0], "Navi";
    assert_eq val[1], "Rust";

    // map
    let val = try! json.parse::<<string, string>>(`{"name": "Jason Lee"}`);
    assert_eq val["name"], "Jason Lee";

    // union type
    let val = try! json.parse::<MyUnionType>(`"foo"`);
    assert_eq val.(string), "foo";
    let val = try! json.parse::<MyUnionType>(`123`);
    assert_eq val.(int), 123;
    let val = try! json.parse::<MyUnionType>(`3.1415`);
    assert_eq val.(float), 3.1415;
    let val = try! json.parse::<MyUnionType>(`true`);
    assert_eq val.(bool), true;

    // Any
    let val = try! json.parse::<Any>(`"foo"`);
    assert_eq val.(string), "foo";
    let val = try! json.parse::<Any>(`123`);
    assert_eq val.(int), 123;
    let val = try! json.parse::<Any>(`true`);
    assert_eq val.(bool), true;
    let val = try! json.parse::<Any>(`1.23`);
    assert_eq val.(float), 1.23;
}

test "parse_struct" {
    let profile = try json.parse::<Profile>(`{
        "foo": "Foo Bar",
        "city": "Chengdu"
    }`);

    assert_eq profile.city, "Chengdu";
}

test "to_string" {
    // nil
    assert_eq try! json.to_string(nil), "null";
    // string
    assert_eq try! json.to_string("foo"), `"foo"`;
    // int
    assert_eq try! json.to_string(123), "123";
    // float
    assert_eq try! json.to_string(123.0), "123.0";
    // bool
    assert_eq try! json.to_string(true), "true";
    assert_eq try! json.to_string(false), "false";
    // array
    assert_eq try! json.to_string([1, 2, 3]), "[1,2,3]";
    assert_eq try! json.to_string(["Navi", "Rust"]), `["Navi","Rust"]`;
    // map
    assert_eq try! json.to_string({"name": "Jason Lee"}), `{"name":"Jason Lee"}`;
    assert_eq try! json.to_string({"vol": 3.12231}), `{"vol":3.12231}`;
    // union type
    let val: MyUnionType = "foo";
    assert_eq try! json.to_string(val), `"foo"`;
    let val: MyUnionType = 123;
    assert_eq try! json.to_string(val), "123";
    let val: MyUnionType = 3.1415;
    assert_eq try! json.to_string(val), "3.1415";
    let val: MyUnionType = true;
    assert_eq try! json.to_string(val), "true";
    // Any
    let val: Any = "foo";
    assert_eq try! json.to_string(val), `"foo"`;
    let val: Any = 123;
    assert_eq try! json.to_string(val), "123";
    let val: Any = 3.1415;
    assert_eq try! json.to_string(val), "3.1415";
    let val: Any = true;
    assert_eq try! json.to_string(val), "true";

    // struct
    let user = try! json.parse::<User>(`{
        "name": "Jason Lee",
        "id": 123456,
        "enabled": true,
        "float": 1.23,
        "profile": {
            "city": "Chengdu"
        },
        "tags": ["Navi", "Rust"],
        "null": null,
        "decimal": "123.456",
        "datetime": "2024-02-28T09:50:40Z",
        "union_type": "foo"
    }`);
    assert_eq try! json.to_string(user), `{"name":"Jason Lee","id":123456,"enabled":true,"float":1.23,"profile":{"city":"Chengdu"},"tags":["Navi","Rust"],"null":null,"decimal":"123.456","datetime":"2024-02-28T09:50:40Z","union_type":"foo"}`;
    assert_eq try! json.to_string(user, pretty: true), `{\n  "name": "Jason Lee",\n  "id": 123456,\n  "enabled": true,\n  "float": 1.23,\n  "profile": {\n    "city": "Chengdu"\n  },\n  "tags": [\n    "Navi",\n    "Rust"\n  ],\n  "null": null,\n  "decimal": "123.456",\n  "datetime": "2024-02-28T09:50:40Z",\n  "union_type": "foo"\n}`;
}

test "from_string" {
    let s = `{ "city": "Chengdu" }`;
    let profile = try! json.from_string::<Profile>(s);
    assert_eq profile.city, "Chengdu";

    let s = `{ "city": "Chengdu" }`;
    let profile = try! json.parse::<Profile>(s);
    assert_eq profile.city, "Chengdu";
}

test "from_reader" {
    let reader = Cursor.new(`{
        "city": "Chengdu"
    }`);
    let profile = try! json.from_reader::<Profile>(reader);
    assert_eq profile.city, "Chengdu";

    let reader = Cursor.new(`{
        "city": "Chengdu"
    }`);
    let profile = try! json.parse::<Profile>(reader);
    assert_eq profile.city, "Chengdu";

    let user = try! json.from_reader::<User>(fs.open(path.join("tests/fixtures", "user.json")));
    assert_eq user.name, "Jason Lee";
}

test "from_bytes" {
    let bytes = `{
        "city": "Chengdu"
    }`.bytes();

    let profile = try! json.from_bytes::<Profile>(bytes);
    assert_eq profile.city, "Chengdu";

    let profile = try! json.parse::<Profile>(bytes);
    assert_eq profile.city, "Chengdu";
}

test "to_writer" {
    let user = User {
        name: "Jason Lee",
        id: 123456,
        enabled: true,
        float: 1.23,
        profile: Profile { city: "Chengdu" },
        tags: ["Navi", "Rust"],
        null: nil,
        decimal: try! Decimal.from_float(123.456),
        datetime: try! DateTime.parse("2024-02-28T09:50:40Z"),
        union_type: 818
    };

    let buf = Bytes.new();
    try! json.to_writer(buf, user);
    assert_eq buf.to_string(), `{"name":"Jason Lee","id":123456,"enabled":true,"float":1.23,"profile":{"city":"Chengdu"},"tags":["Navi","Rust"],"null":null,"decimal":"123.456","datetime":"2024-02-28T09:50:40Z","union_type":818}`;
}
